' Gambas class file

Public modo As Integer 'modo añadir=-1, editar=nº de id
Public anadir As Integer = -1
Public reg As New ClassRegistroAlumno

Public Sub Form_Open()
  
  Dim a As Integer
  Dim diploma As String
  'carga los distintos tipos de diplomas tematicos que haya....
  For a = 0 To ModuleGestionDiploma.lista.Registros.count - 1
    diploma = ModuleGestionDiploma.lista.Registros[a].Id & " | " & ModuleGestionDiploma.lista.Registros[a].NombrePlantilla
    ComboBoxDiplomaTematico.Add(diploma)  
  Next
  
  If modo > -1 Then 
    If reg.FotoAlumno <> "" Then 
      PictureBoxFoto05.Tag = reg.FotoAlumno
      PictureBoxFoto05.Picture = Picture[ModulePrincipal.rutaprograma & "temporales/FotosRegistros/" & reg.FotoAlumno]
    Endif
    
    If reg.Foto07 <> "" Then
      PictureBoxFoto06.Tag = reg.Foto07
      PictureBoxFoto06.Picture = Picture[ModulePrincipal.rutaprograma & "temporales/FotosRegistros/" & reg.Foto07]
    Endif
    
    TextBoxAlumno.text = reg.NombreAlumno
    TextBoxTutor.text = reg.NombreTutor
    TextBoxColegio.text = reg.Colegio
    TextBoxCiudad.text = reg.Ciudad
    TextBoxTag.text = reg.tag
    
    DateBoxFecha.value = Val(reg.Fecha)
    
    ''TODO: valores del combo editado
    ComboBoxDiplomaTematico.Text = Str$(reg.IdDiplomaTematico) & " | " & ModuleGestionDiploma.lista.Registros[ModuleGestionDiploma.lista.buscar(reg.IdDiplomaTematico)].nombreplantilla
    
  Endif
  
  Me.Center
  
End

Public Sub ToolButtonCancelarDiploma2_Click()
  
  Me.close
  
End

Public Sub ToolButtonAceptarDiploma_Click()
  
  reg.IdDiplomaTematico = extraeIdDiploma(ComboBoxDiplomaTematico.text)
  reg.NombreAlumno = TextBoxAlumno.Text
  reg.NombreTutor = TextBoxTutor.Text
  reg.Colegio = TextBoxColegio.Text
  reg.Ciudad = TextBoxCiudad.text
  reg.Fecha = Str$(DateBoxFecha.Value)
  reg.FotoAlumno = PictureBoxFoto05.Tag
  reg.Foto07 = PictureBoxFoto06.Tag
  reg.tag = TextBoxTag.Text
  
  If modo = anadir Then
    ModuleGestionAlumno.anadirRegistro(reg)
  Else
    'editando...
    ModuleGestionAlumno.EditarRegistro(reg, modo)
    
  Endif
  Me.Close
  
End

Private Function extraeIdDiploma(texto As String) As Integer
  
  Dim cadena As String[]
  
  cadena = Split(texto, "|")
  Return Val(cadena[0])
  
End

Public Sub PictureBoxFoto05_MouseDown()
  
  reg.FotoAlumno = asignaImagen(PictureBoxFoto05)
  PictureBoxFoto05.tag = reg.FotoAlumno
  
End

Public Sub PictureBoxFoto06_MouseDown()
  
  reg.Foto07 = asignaImagen(PictureBoxFoto06)
  PictureBoxFoto06.tag = reg.Foto07
  
End

Private Sub asignaImagen(PictureBoxElegido As Picturebox) As String
  ''TODO: Asignando directorio de guardado de imagenes
  
  Dim extension As String
  Dim nombrecopia As String
  Dim comparacion As String 'añadido para comprobar si existe un fichero con el mismo nombre y es igual
  Dim copiar As String
  Dim comando As String
  Dim nombresimple As String
  
  Dialog.Title = "Elija el archivo gráfico"
  Dialog.Filter = ["*.png", "png", "*.jpg", "jpg"]
  If Not Dialog.OpenFile() Then 
    PictureBoxElegido.Picture = Picture[Dialog.Path]
    PictureBoxElegido.tag = Dialog.Path
    nombresimple = ModuleUtilidadesDisco.VerificarNombreEtiqueta(Replace(ModuleUtilidadesDisco.extraedesdebarra(PictureBoxElegido.tag), " ", "_"), ["FOTO", "PLANTILLA", "CAMPO"], ["Imagen", "PLA_TILLA", "CA_PO"])
    
    nombrecopia = ModulePrincipal.RutaPrograma & "temporales/FotosRegistros/" & nombresimple
    copiar = "si"
    While (Exist(nombrecopia))
      'si existe fichero con el mismo nombre, 1º comprobar si es igual...
      comando = "cmp " & nombrecopia & " " & PictureBoxElegido.tag
      Shell comando To comparacion
      If comparacion = "" Then 
        'son iguales-> no hace falta cambiar de nombre ya que lo tengo...
        'no hago la copia
        copiar = "no"
        
        Break
      Endif
      
      'para que no machaque nombres de fichros...
      extension = ModuleUtilidadesDisco.extraeExtension(nombrecopia)
      If extension <> "" Then 
        nombrecopia = Mid$(nombrecopia, 1, Len(nombrecopia) - Len(extension) - 1) & "c" & "." & extension
        copiar = "si"
      Else
        nombrecopia = Mid$(nombrecopia, 1, Len(nombrecopia) - Len(extension)) & "c" 
        copiar = "si"
      Endif
    Wend
    If copiar = "si" Then Exec ["cp", PictureBoxElegido.tag, nombrecopia] Wait
    
    Return ModuleUtilidadesDisco.extraedesdebarra(nombrecopia)
  Endif
  
End

Public Sub botontransparente_Click()
  
  Dim numero As Integer
  Dim boton As Integer
  
  numero = Val(Last.tag)
  
  Select numero
    Case 1
      reg.FotoAlumno = ModuleHacerTransparente.convertirTransparente(ModulePrincipal.RutaPrograma & "temporales/FotosRegistros/" & reg.FotoAlumno)
      PictureBoxFoto05.Picture = Picture[ModulePrincipal.rutaprograma & "temporales/FotosRegistros/" & reg.FotoAlumno]
    Case 2
      reg.Foto07 = ModuleHacerTransparente.convertirTransparente(ModulePrincipal.RutaPrograma & "temporales/FotosRegistros/" & reg.Foto07)
      PictureBoxFoto06.Picture = Picture[ModulePrincipal.rutaprograma & "temporales/FotosRegistros/" & reg.Foto07]
  End Select
  
End
